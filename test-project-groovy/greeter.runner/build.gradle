import org.gradle.util.GradleVersion

plugins {
    id 'application'
}

dependencies {
    implementation project(':greeter.api')
    runtimeOnly project(':greeter.provider')
}

application {
    if(GradleVersion.current().compareTo(GradleVersion.version("6.4")) < 0) {
        mainClassName = "$moduleName/examples.Runner"
    } else {
        mainClass = "examples.Runner"
        mainModule = moduleName
    }
    applicationDefaultJvmArgs = ['-Dgreeter.sender=gradle-modules-plugin']
}

modularity {
    patchModule('java.annotation', 'jsr305-3.0.2.jar')
}
patchModules.config = [
        'java.annotation=jsr250-api-1.0.jar'
]

run {
    moduleOptions {
        addModules = ['java.sql']
    }

    jvmArgs = ['-Dgreeter.sender=gradle-modules-plugin']
}
